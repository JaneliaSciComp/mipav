//----------------------------------------------------------------------------
// Shared functions.
//----------------------------------------------------------------------------
void GetDirectionalLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightDirection,
 float     fSpecularExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor)
{
    float fDiff = 0;
    float fSpec = 0;

    fDiff = -dot(kModelNormal,kLightDirection);
    if (fDiff > 0.0f)
    {
        float3 kViewVector = normalize(kCameraPosition - kModelPosition);
        float3 kHalfVector = normalize(kViewVector - kLightDirection);
        fSpec = dot(kModelNormal,kHalfVector);
        if (fSpec > 0.0f)
        {
            fSpec = pow(fSpec,fSpecularExponent);
        }
        else
        {
            fSpec = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
    }
    
    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
}
//----------------------------------------------------------------------------
void GetPointLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightPosition,
 float     fSpecularExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor)
{
    float fDiff, fSpec;

    float3 kVertexDirection = normalize(kModelPosition - kLightPosition);
    fDiff = -dot(kModelNormal,kVertexDirection);
    if (fDiff > 0.0f)
    {
        float3 kViewVector = normalize(kCameraPosition - kModelPosition);
        float3 kHalfVector = normalize(kViewVector - kVertexDirection);
        fSpec = dot(kModelNormal,kHalfVector);
        if (fSpec > 0.0f)
        {
            fSpec = pow(fSpec,fSpecularExponent);
        }
        else
        {
            fSpec = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
    }

    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
}
//----------------------------------------------------------------------------
void GetSpotLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightPosition,
 float     fSpecularExponent,
 float3    kSpotAxis,
 float     fSpotCosAngle,
 float     fSpotExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor,
 out float fSpotFactor)
{
    float fDiff, fSpec, fSpot;

    float3 kVertexDirection = normalize(kModelPosition - kLightPosition);
    float fVertexCosAngle = dot(kSpotAxis,kVertexDirection);
    if (fVertexCosAngle >= fSpotCosAngle)
    {
        fDiff = -dot(kModelNormal,kVertexDirection);
        if (fDiff > 0.0f)
        {
            float3 kViewVector = normalize(kCameraPosition - kModelPosition);
            float3 kHalfVector = normalize(kViewVector - kVertexDirection);
            fSpec = dot(kModelNormal,kHalfVector);
            if (fSpec > 0.0f)
            {
                fSpec = pow(fSpec,fSpecularExponent);
            }
            else
            {
                fSpec = 0.0f;
            }
            fSpot = pow(fVertexCosAngle,fSpotExponent);
        }
        else
        {
            fDiff = 0.0f;
            fSpec = 0.0f;
            fSpot = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
        fSpot = 0.0f;
    }

    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
    fSpotFactor = fSpot;
}

float GetAttenuation
(
 float3   kModelPos,
 float3   kLightPos, 
 float4   kAttenuation)
{
    // Attenuate the color (x=constant, y=linear, z=quadratic, w=intensity).
    // Attenuation is not active when the x component is zero.  The distance
    // must be computed in *world* coordinates.  The distance in camera
    // coordinates is not correct when the MVP matrix has nonunit scaling
    // factors.
    
    float3 kVertexWorldDir = kModelPos - kLightPos;
    //float3 kVertexModelDir = kModelPos - kLightPos;
    //float3 kVertexWorldDir = mul(kVertexModelDir,kWMatrix);
    float fDistance = sqrt(
                           kVertexWorldDir.x*kVertexWorldDir.x +
                           kVertexWorldDir.y*kVertexWorldDir.y +
                           kVertexWorldDir.z*kVertexWorldDir.z);

    float fAttn = kAttenuation.w/(kAttenuation.x + fDistance*(kAttenuation.y
                                                              + fDistance*kAttenuation.z));
    
    return fAttn;
}

void AmbientLight( uniform float3   MaterialEmissive,
                   uniform float3   MaterialAmbient,
                   uniform float3   LightAmbient,
                   uniform float4   LightAttenuation,
                   out     float4 kVertexColor)

{
    float3 kLAmb = LightAttenuation.w*LightAmbient;
    kVertexColor.rgb = MaterialEmissive + MaterialAmbient*kLAmb;
    kVertexColor.a = 1.0;
}

void DirectionalLight( uniform float3 kModelPosition,
                       uniform float3 kModelNormal,
                       uniform float3   CameraWorldPosition,
                       uniform float3   MaterialEmissive,
                       uniform float3   MaterialAmbient,
                       uniform float4   MaterialDiffuse,
                       uniform float4   MaterialSpecular,
                       uniform float3   LightDirection,
                       uniform float3   LightAmbient,
                       uniform float3   LightDiffuse,
                       uniform float3   LightSpecular,
                       uniform float4   LightAttenuation,
                       out     float4 kVertexColor)
    
{
    float fDiff, fSpec;
    GetDirectionalLightFactors(kModelPosition,kModelNormal,
                               CameraWorldPosition,LightDirection,MaterialSpecular.a,
                               fDiff,fSpec);
    float3 kColor = MaterialAmbient*LightAmbient;
    if (fDiff > 0.0f)
    {
        kColor += fDiff*MaterialDiffuse.rgb*LightDiffuse;
        if (fSpec > 0.0f)
        {
            kColor += fSpec*MaterialSpecular.rgb*LightSpecular;
        }
    }

    kVertexColor.rgb = MaterialEmissive + LightAttenuation.w*kColor;
    kVertexColor.a = 1.0;
}

void PointLight(    uniform float3 kModelPosition,
                    uniform float3 kModelNormal,
                    uniform float3   CameraWorldPosition,
                    uniform float3   MaterialEmissive,
                    uniform float3   MaterialAmbient,
                    uniform float4   MaterialDiffuse,
                    uniform float4   MaterialSpecular,
                    uniform float3   LightWorldPosition,
                    uniform float3   LightAmbient,
                    uniform float3   LightDiffuse,
                    uniform float3   LightSpecular,
                    uniform float4   LightAttenuation,
                    out     float4 kVertexColor)
{
    float fDiff, fSpec;
    GetPointLightFactors(kModelPosition.xyz,kModelNormal,
                         CameraWorldPosition,LightWorldPosition,MaterialSpecular.a,
                         fDiff,fSpec);

    float fAttn = GetAttenuation(kModelPosition.xyz,
                                 LightWorldPosition,LightAttenuation);

    float3 kColor = MaterialAmbient*LightAmbient;
    if (fDiff > 0.0f)
    {
        kColor += fDiff*MaterialDiffuse.xyz*LightDiffuse;
        if (fSpec > 0.0f)
        {
            kColor += fSpec*MaterialSpecular.xyz*LightSpecular;
        }
    }

    kVertexColor.rgb = MaterialEmissive + fAttn*kColor;
    kVertexColor.a = MaterialDiffuse.a;
}

void SpotLight(    uniform float3 kModelPosition,
                   uniform float3 kModelNormal,
                   uniform float3 CameraWorldPosition,
                   uniform float3 MaterialEmissive,
                   uniform float3 MaterialAmbient,
                   uniform float4 MaterialDiffuse,
                   uniform float4 MaterialSpecular,
                   uniform float3 LightWorldPosition,
                   uniform float3 LightWorldDirection,
                   uniform float3 LightAmbient,
                   uniform float3 LightDiffuse,
                   uniform float3 LightSpecular,
                   uniform float4 LightSpotCutoff,
                   uniform float4 LightAttenuation,
                   out     float4 kVertexColor)
{
    float fDiff, fSpec, fSpot;
    GetSpotLightFactors(kModelPosition.xyz,kModelNormal,
                        CameraWorldPosition,LightWorldPosition,MaterialSpecular.a,
                        LightWorldDirection,LightSpotCutoff.y,LightSpotCutoff.w,fDiff,
                        fSpec,fSpot);

    float fAttn = GetAttenuation(kModelPosition.xyz,
                                 LightWorldPosition,LightAttenuation);

    float3 kColor = MaterialAmbient*LightAmbient;
    if (fSpot > 0.0f)
    {
        if (fDiff > 0.0f)
        {
            kColor += (fSpot*fDiff)*MaterialDiffuse.rgb*LightDiffuse;
            if (fSpec > 0.0f)
            {
                kColor += (fSpot*fSpec)*MaterialSpecular.rgb*LightSpecular;
            }
        }
    }
    
    kVertexColor.rgb = MaterialEmissive + fAttn*kColor;
    kVertexColor.a = MaterialDiffuse.a;
}


/**
 * Compute the surface color based on lighting.
 */
void computeColor( float3 kModelPosition, float3 kModelNormal, float3 CameraWorldPosition, 
                   float3 MaterialEmissive, float3 MaterialAmbient, float4 MaterialDiffuse, float4 MaterialSpecular,
                   float4 LightAmbient, float4 LightDiffuse, float4 LightSpecular,
                   float4 LightWorldPosition, float4 LightWorldDirection,
                   float4 LightSpotCutoff, float4 LightAttenuation,
                   float LightType,
                   out float4 color_sample)
{
    if ( LightType == -1 )
    {
        color_sample.r = 0;
        color_sample.g = 0;
        color_sample.b = 0;
    }
    else
    {
        float3 local_normal = kModelNormal.xyz - (0.5, 0.5, 0.5);
        local_normal = normalize( local_normal );

        if ( LightType == 0 )
        {
            AmbientLight( MaterialEmissive,
                          MaterialAmbient,
                          LightAmbient,
                          LightAttenuation,
                          color_sample );
        }
        else if ( LightType == 1 )
        {
            DirectionalLight(  kModelPosition,
                               local_normal,
                               CameraWorldPosition,
                               MaterialEmissive,
                               MaterialAmbient,
                               MaterialDiffuse,
                               MaterialSpecular,
                               LightWorldDirection,
                               LightAmbient,
                               LightDiffuse,
                               LightSpecular,
                               LightAttenuation,
                               color_sample);

        }
        else if ( LightType == 2 )
        {
            PointLight( kModelPosition,
                        local_normal,
                        CameraWorldPosition,
                        MaterialEmissive,
                        MaterialAmbient,
                        MaterialDiffuse,
                        MaterialSpecular,
                        LightWorldPosition,
                        LightAmbient,
                        LightDiffuse,
                        LightSpecular,
                        LightAttenuation,
                        color_sample);
        }
        else
        {
            SpotLight( kModelPosition,
                       local_normal,
                       CameraWorldPosition,
                       MaterialEmissive,
                       MaterialAmbient,
                       MaterialDiffuse,
                       MaterialSpecular,
                       LightWorldPosition,
                       LightWorldDirection,
                       LightAmbient,
                       LightDiffuse,
                       LightSpecular,
                       LightSpotCutoff,
                       LightAttenuation,
                       color_sample);
        }
    }
}

/**
 * Clip the volume based on the x,y,z axes.
 * returns 1 when the volume is clipped, 0 when not clipped.
 */
bool myClip ( float3 myVec,
              float clipX,
              float clipXInv,
              float clipY,
              float clipYInv,
              float clipZ,
              float clipZInv )
{
    if ( myVec.x > clipX )
    {
        return true;
    }
    if ( myVec.x < clipXInv )
    {
        return true;
    }
    if ( myVec.y > clipY )
    {
        return true;
    }
    if ( myVec.y < clipYInv )
    {
        return true;
    }
    if ( myVec.z > clipZ )
    {
        return true;
    }
    if ( myVec.z < clipZInv )
    {
        return true;
    }
    return false;
}


void v_MipavLighting(
                     in float4        kModelPosition  : POSITION,
                     in float3        kModelNormal : NORMAL,

                     out float4       kClipPosition : POSITION,
                     out float4       kVertexColor : COLOR,

                     uniform float3 MaterialEmissive,
                     uniform float3 MaterialAmbient,
                     uniform float4 MaterialDiffuse,
                     uniform float4 MaterialSpecular,
                     
                     uniform float  Light0Type,
                     uniform float4 Light0Ambient,
                     uniform float4 Light0Diffuse,
                     uniform float4 Light0Specular,
                     uniform float4 Light0SpotCutoff,
                     uniform float4 Light0Attenuation,
                     uniform float4 Light0ModelPosition,
                     uniform float4 Light0ModelDirection,
                     
                     uniform float4 Light1Ambient,
                     uniform float4 Light1Attenuation,
                     
                     uniform float  Light2Type,
                     uniform float4 Light2Ambient,
                     uniform float4 Light2Diffuse,
                     uniform float4 Light2Specular,
                     uniform float4 Light2SpotCutoff,
                     uniform float4 Light2Attenuation,
                     uniform float4 Light2WorldPosition,
                     uniform float4 Light2WorldDirection,
                     
                     uniform float  Light3Type,
                     uniform float4 Light3Ambient,
                     uniform float4 Light3Diffuse,
                     uniform float4 Light3Specular,
                     uniform float4 Light3SpotCutoff,
                     uniform float4 Light3Attenuation,
                     uniform float4 Light3WorldPosition,
                     uniform float4 Light3WorldDirection,
                     
                     uniform float3 CameraModelPosition,
                     uniform float3 CameraWorldPosition,
                     uniform float4x4 WMatrix,
                     uniform float4x4 WVPMatrix )

{
    // Transform the position from model space to clip space.
    kClipPosition = mul(kModelPosition,WVPMatrix);

    // First light is static light:
    float4 color0 = (0,0,0,0);
    float4 color1 = (0,0,0,0);
    float4 color2 = (0,0,0,0);
    float4 color3 = (0,0,0,0);
    computeColor( kModelPosition, kModelNormal, CameraModelPosition,
                  MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                  Light0Ambient, Light0Diffuse, Light0Specular,
                  Light0ModelPosition, Light0ModelDirection,
                  Light0SpotCutoff, Light0Attenuation,
                  Light0Type, color0 );

    // Assume second light is alwasy an ambient light:
    AmbientLight( MaterialEmissive,  MaterialAmbient, 
                  Light1Ambient,
                  Light1Attenuation,
                  color1 );
    
    // Remaining lights:
    computeColor( kModelPosition, kModelNormal, CameraModelPosition,
                  MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                  Light2Ambient, Light2Diffuse, Light2Specular,
                  Light2WorldPosition, Light2WorldDirection,
                  Light2SpotCutoff, Light2Attenuation,
                  Light2Type, color2 );
    
    computeColor( kModelPosition, kModelNormal, CameraModelPosition,
                  MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                  Light3Ambient, Light3Diffuse, Light3Specular,
                  Light3WorldPosition, Light3WorldDirection,
                  Light3SpotCutoff, Light3Attenuation,
                  Light3Type, color3 );
    
    kVertexColor = color0 + color1 + color2 + color3;
    kVertexColor.a = 1.0f;
}
