//----------------------------------------------------------------------------
// Shared functions.
//----------------------------------------------------------------------------
void GetDirectionalLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightDirection,
 float     fSpecularExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor)
{
    float fDiff, fSpec;
    kLightDirection = normalize(kLightDirection);
    fDiff = -dot(kModelNormal,kLightDirection);
    if (fDiff > 0.0f)
    {
        float3 kViewVector = normalize(kCameraPosition - kModelPosition);
        float3 kHalfVector = normalize(kViewVector - kLightDirection);
        fSpec = dot(kModelNormal,kHalfVector);
        if (fSpec > 0.0f)
        {
            fSpec = pow(fSpec,fSpecularExponent);
        }
        else
        {
            fSpec = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
    }
    
    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
}
//----------------------------------------------------------------------------
void GetPointLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightPosition,
 float     fSpecularExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor)
{
    float fDiff, fSpec;

    float3 kVertexDirection = normalize(kModelPosition - kLightPosition);
    fDiff = -dot(kModelNormal,kVertexDirection);
    if (fDiff > 0.0f)
    {
        float3 kViewVector = normalize(kCameraPosition - kModelPosition);
        float3 kHalfVector = normalize(kViewVector - kVertexDirection);
        fSpec = dot(kModelNormal,kHalfVector);
        if (fSpec > 0.0f)
        {
            fSpec = pow(fSpec,fSpecularExponent);
        }
        else
        {
            fSpec = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
    }

    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
}
//----------------------------------------------------------------------------
void GetSpotLightFactors
(
 float3    kModelPosition,
 float3    kModelNormal,
 float3    kCameraPosition,
 float3    kLightPosition,
 float     fSpecularExponent,
 float3    kSpotAxis,
 float     fSpotCosAngle,
 float     fSpotExponent,
 out float fDiffuseFactor,
 out float fSpecularFactor,
 out float fSpotFactor)
{
    float fDiff, fSpec, fSpot;

    float3 kVertexDirection = normalize(kModelPosition - kLightPosition);
    float fVertexCosAngle = dot(kSpotAxis,kVertexDirection);
    if (fVertexCosAngle >= fSpotCosAngle)
    {
        fDiff = -dot(kModelNormal,kVertexDirection);
        if (fDiff > 0.0f)
        {
            float3 kViewVector = normalize(kCameraPosition - kModelPosition);
            float3 kHalfVector = normalize(kViewVector - kVertexDirection);
            fSpec = dot(kModelNormal,kHalfVector);
            if (fSpec > 0.0f)
            {
                fSpec = pow(fSpec,fSpecularExponent);
            }
            else
            {
                fSpec = 0.0f;
            }
            fSpot = pow(fVertexCosAngle,fSpotExponent);
        }
        else
        {
            fDiff = 0.0f;
            fSpec = 0.0f;
            fSpot = 0.0f;
        }
    }
    else
    {
        fDiff = 0.0f;
        fSpec = 0.0f;
        fSpot = 0.0f;
    }

    fDiffuseFactor = fDiff;
    fSpecularFactor = fSpec;
    fSpotFactor = fSpot;
}

float GetAttenuation
(
 float3   kModelPos,
 float3   kLightPos, 
 float4   kAttenuation)
{
    // Attenuate the color (x=constant, y=linear, z=quadratic, w=intensity).
    // Attenuation is not active when the x component is zero.  The distance
    // must be computed in *world* coordinates.  The distance in camera
    // coordinates is not correct when the MVP matrix has nonunit scaling
    // factors.
    
    float3 kVertexWorldDir = kModelPos - kLightPos;
    //float3 kVertexModelDir = kModelPos - kLightPos;
    //float3 kVertexWorldDir = mul(kVertexModelDir,kWMatrix);
    float fDistance = sqrt(
                           kVertexWorldDir.x*kVertexWorldDir.x +
                           kVertexWorldDir.y*kVertexWorldDir.y +
                           kVertexWorldDir.z*kVertexWorldDir.z);

    float fAttn = kAttenuation.w/(kAttenuation.x + fDistance*(kAttenuation.y
                                                              + fDistance*kAttenuation.z));
    
    return fAttn;
}

void AmbientLight( uniform float3   MaterialEmissive,
                   uniform float3   MaterialAmbient,
                   uniform float4   LightAmbient,
                   uniform float4   LightAttenuation,
                   out     float3 kVertexColor)

{
    float3 kLAmb = LightAttenuation.w*LightAmbient.xyz;
    kVertexColor.rgb = MaterialEmissive + MaterialAmbient*kLAmb;
}

void DirectionalLight( uniform float4 kModelPosition,
                       uniform float3 kModelNormal,
                       uniform float3   CameraWorldPosition,
                       uniform float3   MaterialEmissive,
                       uniform float3   MaterialAmbient,
                       uniform float4   MaterialDiffuse,
                       uniform float4   MaterialSpecular,
                       uniform float4   LightDirection,
                       uniform float4   LightAmbient,
                       uniform float4   LightDiffuse,
                       uniform float4   LightSpecular,
                       uniform float4   LightAttenuation,
                       out     float3 kVertexColor)
    
{
    float fDiff, fSpec;
    GetDirectionalLightFactors((float3)kModelPosition,kModelNormal,
                               CameraWorldPosition,LightDirection.xyz,MaterialSpecular.a,
                               fDiff,fSpec);

    float3 kColor = MaterialAmbient*LightAmbient.xyz;
    if (fDiff > 0.0f)
    {
        kColor += fDiff*MaterialDiffuse.rgb*LightDiffuse.xyz;
        if (fSpec > 0.0f)
        {
            kColor += fSpec*MaterialSpecular.rgb*LightSpecular.xyz;
        }
    }

    kVertexColor.rgb = MaterialEmissive + LightAttenuation.w*kColor;
}

void PointLight(    uniform float4 kModelPosition,
                    uniform float3 kModelNormal,
                    uniform float3   CameraWorldPosition,
                    uniform float3   MaterialEmissive,
                    uniform float3   MaterialAmbient,
                    uniform float4   MaterialDiffuse,
                    uniform float4   MaterialSpecular,
                    uniform float4   LightWorldPosition,
                    uniform float4   LightAmbient,
                    uniform float4   LightDiffuse,
                    uniform float4   LightSpecular,
                    uniform float4   LightAttenuation,
                    out     float3 kVertexColor)
{
    float fDiff, fSpec;
    GetPointLightFactors(kModelPosition.xyz,kModelNormal,
                         CameraWorldPosition,LightWorldPosition.xyz,MaterialSpecular.a,
                         fDiff,fSpec);

    float fAttn = GetAttenuation(kModelPosition.xyz,
                                 LightWorldPosition.xyz,LightAttenuation);

    float3 kColor = MaterialAmbient*LightAmbient.xyz;
    if (fDiff > 0.0f)
    {
        kColor += fDiff*MaterialDiffuse.xyz*LightDiffuse.xyz;
        if (fSpec > 0.0f)
        {
            kColor += fSpec*MaterialSpecular.xyz*LightSpecular.xyz;
        }
    }

    kVertexColor.rgb = MaterialEmissive + fAttn*kColor;
}

void SpotLight(    uniform float4 kModelPosition,
                   uniform float3 kModelNormal,
                   uniform float3 CameraWorldPosition,
                   uniform float3 MaterialEmissive,
                   uniform float3 MaterialAmbient,
                   uniform float4 MaterialDiffuse,
                   uniform float4 MaterialSpecular,
                   uniform float4 LightWorldPosition,
                   uniform float4 LightWorldDirection,
                   uniform float4 LightAmbient,
                   uniform float4 LightDiffuse,
                   uniform float4 LightSpecular,
                   uniform float4 LightSpotCutoff,
                   uniform float4 LightAttenuation,
                   out     float3 kVertexColor)
{
    float fDiff, fSpec, fSpot;
    GetSpotLightFactors(kModelPosition.xyz,kModelNormal,
                        CameraWorldPosition,LightWorldPosition.xyz,MaterialSpecular.a,
                        LightWorldDirection.xyz,LightSpotCutoff.y,LightSpotCutoff.w,
                        fDiff,fSpec,fSpot);

    float fAttn = GetAttenuation(kModelPosition.xyz,
                                 LightWorldPosition.xyz,LightAttenuation);

    float3 kColor = MaterialAmbient*LightAmbient.xyz;
    if (fSpot > 0.0f)
    {
        if (fDiff > 0.0f)
        {
            kColor += (fSpot*fDiff)*MaterialDiffuse.rgb*LightDiffuse.xyz;
            if (fSpec > 0.0f)
            {
                kColor += (fSpot*fSpec)*MaterialSpecular.rgb*LightSpecular.xyz;
            }
        }
    }
    
    kVertexColor.rgb = MaterialEmissive + fAttn*kColor;
}


/**
 * Compute the surface color based on lighting.
 */
void computeColor( float4 kModelPosition, float3 kModelNormal, float3 CameraWorldPosition, 
                   float3 MaterialEmissive, float3 MaterialAmbient, float4 MaterialDiffuse, float4 MaterialSpecular,
                   float4 LightAmbient, float4 LightDiffuse, float4 LightSpecular,
                   float4 LightWorldPosition, float4 LightWorldDirection,
                   float4 LightSpotCutoff, float4 LightAttenuation,
                   float LightType,
                   float Composite,
                   inout float3 color_sample)
{
    if ( LightType == -1 )
    {
        color_sample.r = 0;
        color_sample.g = 0;
        color_sample.b = 0;
    }
    else
    {
        float3 local_normal = kModelNormal.xyz - (0.5, 0.5, 0.5);
        local_normal = normalize( local_normal );

        float3 LocalMaterialAmbient = MaterialAmbient;
        float4 LocalMaterialDiffuse = MaterialDiffuse;
        float3 LocalMaterialEmissive = MaterialEmissive;
        float4 LocalMaterialSpecular = MaterialSpecular;

        if ( Composite != 0 )
        {
            LocalMaterialAmbient = color_sample.rgb * MaterialAmbient;
            LocalMaterialDiffuse.rgb = color_sample * MaterialDiffuse.rgb;
            LocalMaterialEmissive = color_sample.rgb * MaterialEmissive;
            LocalMaterialSpecular.rgb = color_sample * MaterialSpecular.rgb;
        }

        if ( LightType == 0 )
        {
            AmbientLight( LocalMaterialEmissive,
                          LocalMaterialAmbient,
                          LightAmbient,
                          LightAttenuation,
                          color_sample );
        }
        else if ( LightType == 1 )
        {
            DirectionalLight(  kModelPosition,
                               local_normal,
                               CameraWorldPosition,
                               LocalMaterialEmissive,
                               LocalMaterialAmbient,
                               LocalMaterialDiffuse,
                               LocalMaterialSpecular,
                               LightWorldDirection,
                               LightAmbient,
                               LightDiffuse,
                               LightSpecular,
                               LightAttenuation,
                               color_sample);

        }
        else if ( LightType == 2 )
        {
            PointLight( kModelPosition,
                        local_normal,
                        CameraWorldPosition,
                        LocalMaterialEmissive,
                        LocalMaterialAmbient,
                        LocalMaterialDiffuse,
                        LocalMaterialSpecular,
                        LightWorldPosition,
                        LightAmbient,
                        LightDiffuse,
                        LightSpecular,
                        LightAttenuation,
                        color_sample);
        }
        else
        {
            SpotLight( kModelPosition,
                       local_normal,
                       CameraWorldPosition,
                       LocalMaterialEmissive,
                       LocalMaterialAmbient,
                       LocalMaterialDiffuse,
                       LocalMaterialSpecular,
                       LightWorldPosition,
                       LightWorldDirection,
                       LightAmbient,
                       LightDiffuse,
                       LightSpecular,
                       LightSpotCutoff,
                       LightAttenuation,
                       color_sample);
        }
    }
}

/**
 * Clip the volume based on the x,y,z axes.
 * returns 1 when the volume is clipped, 0 when not clipped.
 */
bool myClip(float3 vec,
            float clipX,
            float clipXInv,
            float clipY,
            float clipYInv,
            float clipZ,
            float clipZInv )
{
    if ( vec.x > clipX )
    {
        return true;
    }
    if ( vec.x < clipXInv )
    {
        return true;
    }
    if ( vec.y > clipY )
    {
        return true;
    }
    if ( vec.y < clipYInv )
    {
        return true;
    }
    if ( vec.z > clipZ )
    {
        return true;
    }
    if ( vec.z < clipZInv )
    {
        return true;
    }
    return false;
}


/** Raycasting fragment program implementation */
void p_VolumeShaderSUR_SUR(  in float3 inTexCoord1    : TEXCOORD0,
                             in float4 inPos,
                             out float4 kColor	    : COLOR0,
                             uniform sampler2D tex, 
                             uniform sampler3D volume_texA, 
                             uniform sampler1D colormapA, 
                             uniform sampler1D opacitymapA, 
                             uniform sampler3D normalmapA, 
                             uniform sampler3D volume_texB, 
                             uniform sampler1D colormapB, 
                             uniform sampler1D opacitymapB, 
                             uniform sampler3D normalmapB, 
                             uniform float stepsize,
                             uniform float blend,
                             uniform float IsColorA,
                             uniform float IsColorB,
                             uniform float DoClip,
                             uniform float CompositeA,
                             uniform float CompositeB,
                             uniform float clipX,
                             uniform float clipXInv,
                             uniform float clipY,
                             uniform float clipYInv,
                             uniform float clipZ,
                             uniform float clipZInv,
                             uniform float4 clipArb,
                             uniform float4 clipEye,
                             uniform float4 clipEyeInv,

                             uniform float3 MaterialEmissive,
                             uniform float3 MaterialAmbient,
                             uniform float4 MaterialDiffuse,
                             uniform float4 MaterialSpecular,

                             uniform float  Light0Type,
                             uniform float4 Light0Ambient,
                             uniform float4 Light0Diffuse,
                             uniform float4 Light0Specular,
                             uniform float4 Light0SpotCutoff,
                             uniform float4 Light0Attenuation,
                             uniform float4 Light0WorldPosition,
                             uniform float4 Light0WorldDirection,

                             uniform float4 Light1Ambient,
                             uniform float4 Light1Attenuation,

                             uniform float  Light2Type,
                             uniform float4 Light2Ambient,
                             uniform float4 Light2Diffuse,
                             uniform float4 Light2Specular,
                             uniform float4 Light2SpotCutoff,
                             uniform float4 Light2Attenuation,
                             uniform float4 Light2WorldPosition,
                             uniform float4 Light2WorldDirection,

                             uniform float  Light3Type,
                             uniform float4 Light3Ambient,
                             uniform float4 Light3Diffuse,
                             uniform float4 Light3Specular,
                             uniform float4 Light3SpotCutoff,
                             uniform float4 Light3Attenuation,
                             uniform float4 Light3WorldPosition,
                             uniform float4 Light3WorldDirection,

                             uniform float3 CameraWorldPosition,
                             uniform float4x4 WVMatrix,
                             uniform float4x4 WVPMatrix)
{
    // find the right place to lookup in the backside buffer
    float2 texc = ((inPos.xy / inPos.w) + 1) / 2.0;
    float4 back_position  = tex2D(tex, texc);

    // the start position of the ray is stored in the texturecoordinate
    float3 start = inTexCoord1; 

    // the ray direction
    float3 dir = back_position.xyz - start;

    // the length from front to back is calculated and used to terminate the ray
    float len = length(dir.xyz); 

    // normalized direction vector:
    float3 norm_dir = normalize(dir);

    // current position along the ray: 
    float3 position = start.xyz;

    // The accumulated color and alpha values:
    float4 color_accA = float4(0,0,0,0);
    float alpha_accA = 0;

    // The accumulated color and alpha values:
    float4 color_accB = float4(0,0,0,0);
    float alpha_accB = 0;

    // output color:
    kColor = (0,0,0,0);
    if ( (back_position.x == 0) && (back_position.y == 0) && (back_position.z == 0) )
    {
        return;
    }

    // limit the number of iterations to 450, make sure that the stepsize will
    // cover the entire ray:
    if ( (len/stepsize) > 450 )
    {
        stepsize = len/450.0;
    }

    // The color at the current position along the ray:
    float4 colorA = (0,0,0,0);
    // The opacity at the current position along the ray:
    float opacityA = 0;

    // The color at the current position along the ray:
    float4 colorB = (0,0,0,0);
    // The opacity at the current position along the ray:
    float opacityB = 0;

    // The normal at the current position along the ray:
    float4 normal = (0,0,0,0);

    float alpha_sample = stepsize;
    float delta = stepsize;
    float3 delta_dir = norm_dir * delta;
    float delta_dir_len = length(delta_dir);
    float length_acc = 0;
    bool bClipped = false;

    bool breakA = false;
    bool breakB = false;

    float4 worldNormal;
    float3 color0;
    float3 color1;
    float3 color2;
    float3 color3;

    // For some profiles the number of loop iterations must be determined at
    // compile time:
    for( int i = 0; i < 450; i++ )
    {
        // axis-aligned clipping:
        if ( (DoClip != 0) && myClip( position, clipX, clipXInv, clipY, clipYInv, clipZ, clipZInv ) )
        {
            colorA = (0,0,0,0);
            opacityA = 0;

            colorB = (0,0,0,0);
            opacityB = 0;
        }
        else
        {
            bClipped = false;
            if ( DoClip != 0 )
            {
                // eye clipping and arbitrary clipping:
                float4 aPosition = float4(0,0,0,0);
                aPosition.xyz = position.xyz - (.5,.5,.5);
                aPosition = mul( aPosition, WVPMatrix );
                aPosition.xyz = aPosition.xyz + (.5,.5,.5);
                float fDot = dot( aPosition.xyz, clipEye.xyz );
                float fDotInv = dot( aPosition.xyz, clipEyeInv.xyz );
                float fDotArb = dot( position.xyz, clipArb.xyz );
                if ( (fDot < clipEye.w) || (fDotInv > clipEyeInv.w) || (fDotArb > clipArb.w) )
                {
                    colorA = (0,0,0,0);
                    opacityA = 0;

                    colorB = (0,0,0,0);
                    opacityB = 0;
                    bClipped = true;
                }
            }
            // The value is not clipped, compute the color:
            if ( !bClipped )
            {
                colorA = tex3D(volume_texA,position);
                opacityA = tex1D(opacitymapA,colorA.r).r;
                if ( IsColorA != 0 )
                {
                    colorA.r = tex1D(colormapA,colorA.r).r;
                    colorA.g = tex1D(colormapA,colorA.g).g;
                    colorA.b = tex1D(colormapA,colorA.b).b;
                }
                else
                {
                    colorA = tex1D(colormapA,colorA.r);
                }

                colorB = tex3D(volume_texB,position);
                opacityB = tex1D(opacitymapB,colorB.r).r;
                if ( IsColorB != 0 )
                {
                    colorB.r = tex1D(colormapB,colorB.r).r;
                    colorB.g = tex1D(colormapB,colorB.g).g;
                    colorB.b = tex1D(colormapB,colorB.b).b;
                }
                else
                {
                    colorB = tex1D(colormapB,colorB.r);
                }
            }
        }
        if ( (opacityA > 0) && !breakA )
        {
            // Surface and Composite surface display:
            normal = tex3D(normalmapA,position);

            // Get the color based on lighting:
            normal.w = 1;
            worldNormal = mul( normal, WVMatrix );
            color0 = colorA.rgb;
            computeColor( inPos, worldNormal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light0Ambient, Light0Diffuse, Light0Specular,
                          Light0WorldPosition, Light0WorldDirection,
                          Light0SpotCutoff, Light0Attenuation,
                          Light0Type,
                          CompositeA, color0 );
                

            normal.w = 0;
            color1 = colorA.rgb;
            computeColor( inPos, normal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light1Ambient, Light0Diffuse, Light0Specular,
                          Light0WorldPosition, Light0WorldDirection,
                          Light0SpotCutoff, Light1Attenuation,
                          0, CompositeA, color1 );

            color2 = colorA.rgb;
            computeColor( inPos, normal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light2Ambient, Light2Diffuse, Light2Specular,
                          Light2WorldPosition, Light2WorldDirection,
                          Light2SpotCutoff, Light2Attenuation,
                          Light2Type,
                          CompositeA, color2 );

            color3 = colorA.rgb;
            computeColor( inPos, normal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light3Ambient, Light3Diffuse, Light3Specular,
                          Light3WorldPosition, Light3WorldDirection,
                          Light3SpotCutoff, Light3Attenuation,
                          Light3Type,
                          CompositeA, color3 );

            colorA.rgb = color0 + color1 + color2 + color3;

            // Composite surface:
            if ( CompositeA != 0 )
            {
                color_accA   = (1.0 - alpha_accA) * colorA * opacityA + color_accA;
                alpha_accA += (1 - alpha_accA) * opacityA;
            }
            // Surface:
            else if ( (colorA.x != 0) || (colorA.y != 0) || (colorA.z != 0) )
            {
                color_accA   = colorA;
                breakA = true;
            }
        }
        // If the opacity is not zero:
        if ( (opacityB > 0) && !breakB )
        {
            // Surface and Composite surface display:
            normal = tex3D(normalmapB,position);

            // Get the color based on lighting:
            normal.w = 1;
            worldNormal = mul( normal, WVMatrix );
            color0 = colorB.rgb;
            computeColor( inPos, worldNormal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light0Ambient, Light0Diffuse, Light0Specular,
                          Light0WorldPosition, Light0WorldDirection,
                          Light0SpotCutoff, Light0Attenuation,
                          Light0Type,
                          CompositeB, color0 );
                

            normal.w = 0;
            color1 = colorB.rgb;
            computeColor( inPos, normal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light1Ambient, Light0Diffuse, Light0Specular,
                          Light0WorldPosition, Light0WorldDirection,
                          Light0SpotCutoff, Light1Attenuation,
                          0, CompositeB, color1 );

            color2 = colorB.rgb;
            computeColor( inPos, normal.xyz, CameraWorldPosition,
                          MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
                          Light2Ambient, Light2Diffuse, Light2Specular,
                          Light2WorldPosition, Light2WorldDirection,
                          Light2SpotCutoff, Light2Attenuation,
                          Light2Type,
                          CompositeB, color2 );

//             color3 = colorB.rgb;
//             computeColor( inPos, normal.xyz, CameraWorldPosition,
//                           MaterialEmissive,  MaterialAmbient, MaterialDiffuse, MaterialSpecular,
//                           Light3Ambient, Light3Diffuse, Light3Specular,
//                           Light3WorldPosition, Light3WorldDirection,
//                           Light3SpotCutoff, Light3Attenuation,
//                           Light3Type,
//                           CompositeB, color3 );

            colorB.rgb = color0 + color1 + color2;// + color3;

            // Composite surface:
            if ( CompositeB != 0 )
            {
                color_accB   = (1.0 - alpha_accB) * colorB * opacityB + color_accB;
                alpha_accB += (1 - alpha_accB) * opacityB;
            }
            // Surface:
            else if ( (colorB.x != 0) || (colorB.y != 0) || (colorB.z != 0) )
            {
                color_accB   = colorB;
                breakB = true;
            }
        }

        // Break early if the accumulate alpha value for composite or composite surface is >= 1
        if ( (CompositeA != 0) && (alpha_accA >= 1.0) )
        {
            breakA = true;
        }
        if ( (CompositeB != 0) && (alpha_accB >= 1.0) )
        {
            breakB = true;
        }
        // Increment position along the ray:
        position += delta_dir;
        length_acc += delta_dir_len;
        // Break when the end of the ray is reached, or if alpha >= 1;
        if ( length_acc >= len )
        {
            break;
        }
    } 

    kColor.rgb = blend * color_accA.rgb + (1 - blend) * color_accB.rgb;

    if ( blend > 3 )
    {
        kColor = saturate(color_accA + color_accB );
    }
    else if ( blend > 2 )
    {
        kColor = color_accA * color_accB;
    }
    else if ( blend > 1 )
    {
        kColor = (1.0f - color_accA)*color_accB + color_accA;
    }
    kColor.a = 1;
}
