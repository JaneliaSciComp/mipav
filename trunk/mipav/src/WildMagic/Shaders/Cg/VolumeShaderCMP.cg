/**
 * Clip the volume based on the x,y,z axes.
 * returns 1 when the volume is clipped, 0 when not clipped.
 */
bool myClip(float3 vec,
            float clipX,
            float clipXInv,
            float clipY,
            float clipYInv,
            float clipZ,
            float clipZInv )
{
    if ( vec.x > clipX )
    {
        return 1;
    }
    if ( vec.x < clipXInv )
    {
        return 1;
    }
    if ( vec.y > clipY )
    {
        return 1;
    }
    if ( vec.y < clipYInv )
    {
        return 1;
    }
    if ( vec.z > clipZ )
    {
        return 1;
    }
    if ( vec.z < clipZInv )
    {
        return 1;
    }
    return 0;
}

/** Raycasting fragment program implementation */
void p_VolumeShaderCMP(  in float3 inTexCoord1    : TEXCOORD0,
                         in float4 inPos,
                         out float4 kColor	    : COLOR0,
                         uniform sampler2D SceneImage : TEXUNIT0, 

                         uniform sampler3D VolumeImageA : TEXUNIT1, 
                         uniform sampler1D ColorMapA : TEXUNIT2, 
                         uniform sampler1D OpacityMapA : TEXUNIT3, 
                         uniform sampler3D NormalMapA: TEXUNIT4, 
                         uniform sampler3D VolumeImageGMA: TEXUNIT5, 
                         uniform sampler1D OpacityMapA_GM : TEXUNIT6, 
                         uniform sampler3D VolumeImageGMGMA: TEXUNIT7, 

                         uniform sampler3D VolumeImageB : TEXUNIT8, 
                         uniform sampler1D ColorMapB : TEXUNIT9, 
                         uniform sampler1D OpacityMapB : TEXUNIT10, 
                         uniform sampler3D NormalMapB: TEXUNIT11, 
                         uniform sampler3D VolumeImageGMB: TEXUNIT12, 
                         uniform sampler1D OpacityMapB_GM : TEXUNIT13, 
                         uniform sampler3D VolumeImageGMGMB: TEXUNIT14, 

                         uniform float ShowB,
                         uniform float ABBlend,
                         uniform float2 MinMax,
                         uniform float blend,
                         uniform float stepsize,
                         uniform float4 steps,
                         uniform float IsColor,
                         uniform float IsColorB,
                         uniform float DoClip,
                         uniform float GradientMagnitude,
                         uniform float GradientMagnitudeB,
                         uniform float4 BackgroundColor,
                         uniform float clipX,
                         uniform float clipXInv,
                         uniform float clipY,
                         uniform float clipYInv,
                         uniform float clipZ,
                         uniform float clipZInv,
                         uniform float4 clipArb,
                         uniform float4 clipEye,
                         uniform float4 clipEyeInv,
                         uniform float4x4 WVPMatrix)
{
    // find the right place to lookup in the backside buffer
    float2 texc = ((inPos.xy / inPos.w) + 1) / 2.0;
    float4 back_position  = tex2D(SceneImage, texc);

    // the start position of the ray is stored in the texturecoordinate
    float3 start = inTexCoord1; 

    // the ray direction
    float3 dir = back_position.xyz - start;

    // the length from front to back is calculated and used to terminate the ray
    float len = length(dir.xyz); 

    // normalized direction vector:
    float3 norm_dir = normalize(dir);

    // current position along the ray: 
    float3 position = start.xyz;

    // The accumulated color and alpha values:
    float4 color_acc = float4(0,0,0,0);
    float alpha_acc = 0;

    float4 color_accB = float4(0,0,0,0);
    float alpha_accB = 0;

    // output color:
    kColor = BackgroundColor;
    if ( (back_position.x == 0) && (back_position.y == 0) && (back_position.z == 0) )
    {
        return;
    }

    // limit the number of iterations to STEPS, make sure that the stepsize will
    // cover the entire ray:
    if ( (len/stepsize) > steps[0] )
    {
        stepsize = len/steps[0];
    }

    // The color at the current position along the ray:
    float4 color = (0,0,0,0);
    float4 colorB = (0,0,0,0);

    // The opacity at the current position along the ray:
    float opacity = 0;
    float opacityB = 0;

    // Gradient magnitude values along the ray:
    float colorGM = 0;
    float opacityGM = 0;
    float colorGM_B = 0;
    float opacityGM_B = 0;

    float alpha_sample = stepsize;
    float delta = stepsize;
    float3 delta_dir = norm_dir * delta;
    float delta_dir_len = length(delta_dir);
    float length_acc = 0;
    bool bClipped = false;

    // For some profiles the number of loop iterations must be determined at
    // compile time:
    //for( int i = 0; i < 450; i++ )
    for( int i = 0; i < 1; i++ )
    {
        // axis-aligned clipping:
        if ( (DoClip != 0) && myClip( position, clipX, clipXInv, clipY, clipYInv, clipZ, clipZInv ) )
        {
            color = (0,0,0,0);
            opacity = 0;
            colorB = (0,0,0,0);
            opacityB = 0;
        }
        else
        {
            bClipped = false;
            if ( DoClip != 0 )
            {
                // eye clipping and arbitrary clipping:
                float4 aPosition = float4(0,0,0,0);
                aPosition.xyz = position.xyz - (.5,.5,.5);
                aPosition = mul( aPosition, WVPMatrix );
                aPosition.xyz = aPosition.xyz + (.5,.5,.5);
                float fDot = dot( aPosition.xyz, clipEye.xyz );
                float fDotInv = dot( aPosition.xyz, clipEyeInv.xyz );
                float fDotArb = dot( position.xyz, clipArb.xyz );
                if ( (fDot < clipEye.w) || (fDotInv > clipEyeInv.w) || (fDotArb > clipArb.w) )
                {
                    color = (0,0,0,0);
                    opacity = 0;
                    colorB = (0,0,0,0);
                    opacityB = 0;
                    bClipped = true;
                }
            }
            // The value is not clipped, compute the color:
            if ( !bClipped )
            {
                color = tex3D(VolumeImageA,position);
                opacity = tex1D(OpacityMapA,color.r).r;
                if ( GradientMagnitude != 0 )
                {
                    colorGM = tex3D(VolumeImageGMA,position).r;
                    opacityGM = tex1D(OpacityMapA_GM,colorGM).r;
                    opacity = opacity * opacityGM;
                }

                if ( IsColor != 0 )
                {
                    color.r = tex1D(ColorMapA,color.r).r;
                    color.g = tex1D(ColorMapA,color.g).g;
                    color.b = tex1D(ColorMapA,color.b).b;
                }
                else
                {
                    color = tex1D(ColorMapA,color.r);
                }
                
                if ( ShowB != 0)
                {
                    colorB = tex3D(VolumeImageB,position);
                    opacityB = tex1D(OpacityMapB,colorB.r).r;
                    if ( GradientMagnitudeB != 0 )
                    {
                        colorGM_B = tex3D(VolumeImageGMB,position).a;
                        opacityGM_B = tex1D(OpacityMapB_GM,colorGM_B).r;
                        opacityB = opacityB * opacityGM_B;
                    }
                    if ( IsColorB != 0 )
                    {
                        colorB.r = tex1D(ColorMapB,colorB.r).r;
                        colorB.g = tex1D(ColorMapB,colorB.g).g;
                        colorB.b = tex1D(ColorMapB,colorB.b).b;
                    }
                    else
                    {
                        colorB = tex1D(ColorMapB,colorB.r);
                    }
                }
            }
        }
        // If the opacity is not zero:
        if ( opacity > 0 )
        {
            color_acc += (1.0 - alpha_acc) * color * opacity;
            alpha_acc += (1 - alpha_acc) * opacity;
        }
        if ( opacityB > 0 )
        {
            color_accB += (1.0 - alpha_accB) * colorB * opacityB;
            alpha_accB += (1 - alpha_accB) * opacityB;
        }

        // Increment position along the ray:
        position += delta_dir;
        length_acc += delta_dir_len;
        // Break when the end of the ray is reached, or if alpha >= 1;
        if ( length_acc >= len )
        {
            break;
        }
        if ( (ShowB == 0) && (alpha_acc >= 1.0) )
        {
            break;
        }
        else if ( (ShowB != 0) && (alpha_acc >= 1.0) && (alpha_accB >= 1.0) )
        {
            break;
        }

    } 

    if ( ShowB != 0 )
    {
        kColor.rgb = ABBlend * color_acc.rgb + (1 - ABBlend) * color_accB.rgb;
        float fAlpha = ABBlend * alpha_acc + (1 - ABBlend) * alpha_accB;
        kColor.rgb = fAlpha * kColor.rgb + (1-fAlpha)*BackgroundColor.rgb;
        kColor.a = blend;
    }
    else
    {
        kColor.rgb = alpha_acc * color_acc.rgb + (1-alpha_acc)*BackgroundColor.rgb;
        kColor.a = blend * alpha_acc;
    }
}
